apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: 'idea'

repositories {
    jcenter()
}

project.ext.db = file("$buildDir/baseball.mv.db")

task generateTestDb(dependsOn:classes, type:JavaExec) {
    onlyIf { !db.exists() }
    main = 'geocoder.populate_stadium_data'
    classpath sourceSets.main.runtimeClasspath
}

compileTestGroovy.dependsOn generateTestDb

ext.springVersion = '5.0.7.RELEASE'
ext.spockVersion = '1.1-groovy-2.4'

test {
    useJUnitPlatform()
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:2.5.1"
    implementation "org.springframework:spring-context:$springVersion"
    implementation "org.springframework:spring-jdbc:$springVersion"

    // https://mvnrepository.com/artifact/org.hamcrest/hamcrest-library
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.2.0'

    // Java EE (needed for Java 9+ only)
    if (JavaVersion.current() > JavaVersion.VERSION_1_8) {
        implementation 'javax.xml.bind:jaxb-api:2.3.0'
        implementation 'com.sun.xml.bind:jaxb-core:2.3.0'
        implementation 'com.sun.xml.bind:jaxb-impl:2.3.0'
        implementation 'com.sun.activation:javax.activation:1.2.0'
    }

    // Spring 5
    testImplementation "org.spockframework:spock-core:$spockVersion"
    testImplementation "org.springframework:spring-test:$springVersion"
    testImplementation "org.spockframework:spock-spring:$spockVersion"

    // Jersey
    implementation 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.27'
    implementation 'org.glassfish.jersey.inject:jersey-hk2:2.27'
    implementation 'org.glassfish.jersey.media:jersey-media-json-jackson:2.27'

    // REST client
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    // Mocking libraries
    testImplementation 'net.bytebuddy:byte-buddy-dep:1.8.12'
    testImplementation 'org.objenesis:objenesis:2.6'

    // Database
    runtime 'com.h2database:h2:1.4.187'
}
